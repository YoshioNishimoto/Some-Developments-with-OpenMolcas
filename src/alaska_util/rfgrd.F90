!***********************************************************************
! This file is part of OpenMolcas.                                     *
!                                                                      *
! OpenMolcas is free software; you can redistribute it and/or modify   *
! it under the terms of the GNU Lesser General Public License, v. 2.1. *
! OpenMolcas is distributed in the hope that it will be useful, but it *
! is provided "as is" and without any express or implied warranties.   *
! For more details see the full text of the license in the file        *
! LICENSE or in <http://www.gnu.org/licenses/>.                        *
!                                                                      *
! Copyright (C) 1990,1992,1995, Roland Lindh                           *
!               1990, IBM                                              *
!***********************************************************************

subroutine RFGrd( &
#                define _CALLING_
#                include "grd_interface.fh"
                )
!***********************************************************************
!                                                                      *
! Object: to compute the multipole moments integrals with the          *
!         Gauss-Hermite quadrature.                                    *
!                                                                      *
!     Author: Roland Lindh, IBM Almaden Research Center, San Jose, CA  *
!             November '90                                             *
!             Modified to multipole moments November '90               *
!                                                                      *
!             Roland Lindh, Dept. of Theoretical Chemistry, University *
!             of Lund, SWEDEN.                                         *
!             Modified to reaction field calculations July '92         *
!             Modified to gradient calculations May '95                *
!***********************************************************************

use Her_RW, only: HerR, HerW, iHerR, iHerW
use PCM_arrays, only: MM
use Center_Info, only: dc
use Index_Functions, only: nTri_Elem1
use Constants, only: Half
use Definitions, only: wp, iwp, u6

implicit none
#include "grd_interface.fh"
integer(kind=iwp) :: iAlpha, iBeta, ip, ipAlph, ipAxyz, ipBeta, ipBxyz, iPrint, ipRnxyz, ipRxyz, ipTemp1, ipTemp2, ipTemp3, iRout, &
                     iZeta, nip
logical(kind=iwp) :: ABeq(3)
#include "print.fh"

#include "macros.fh"
unused_var(ZInv)
unused_var(iStabM)
unused_var(nStabM)

iRout = 122
iPrint = nPrint(iRout)
!iPrint = 99
ABeq(:) = A == RB

nip = 1
ipAxyz = nip
nip = nip+nZeta*3*nHer*(la+2)
ipBxyz = nip
nip = nip+nZeta*3*nHer*(lb+2)
ipRxyz = nip
nip = nip+nZeta*3*nHer*(nOrdOp+1)
ipRnxyz = nip
nip = nip+nZeta*3*(la+2)*(lb+2)*(nOrdOp+1)
ipTemp1 = nip
nip = nip+nZeta
ipTemp2 = nip
nip = nip+nZeta
ipTemp3 = nip
nip = nip+3*nZeta*nHer
ipAlph = nip
nip = nip+nZeta
ipBeta = nip
nip = nip+nZeta
if (nip-1 > nArr*nZeta) then
  write(u6,*) ' nArr is Wrong! ',nip-1,' > ',nArr*nZeta
  write(u6,*) ' Abend in RFGrd'
  call Abend()
end if

if (iPrint >= 49) then
  call RecPrt(' In RFGrd: A',' ',A,1,3)
  call RecPrt(' In RFGrd: RB',' ',RB,1,3)
  call RecPrt(' In RFGrd: CCoor',' ',CCoor,1,3)
  call RecPrt(' In RFGrd: P',' ',P,nZeta,3)
  write(u6,*) ' In RFGrd: la,lb=',la,lb
  write(u6,*) ' In RFGrd: nHer=',nHer
end if

! Compute the cartesian values of the basis functions angular part

do iZeta=1,nZeta
  Array(ipTemp1-1+iZeta) = Zeta(iZeta)**(-Half)
end do

call vCrtCmp(Array(ipTemp1),P,nZeta,A,Array(ipAxyz),la+1,HerR(iHerR(nHer)),nHer,ABeq)
call vCrtCmp(Array(ipTemp1),P,nZeta,RB,Array(ipBxyz),lb+1,HerR(iHerR(nHer)),nHer,ABeq)

! Compute the contribution from the multipole moment operator

ABeq(:) = .false.
call vCrtCmp(Array(ipTemp1),P,nZeta,Ccoor,Array(ipRxyz),nOrdOp,HerR(iHerR(nHer)),nHer,ABeq)

! Compute the cartesian components for the multipole moment
! integrals. The integrals are factorized into components.

call vAssmbl(Array(ipRnxyz),Array(ipAxyz),la+1,Array(ipRxyz),nOrdOp,Array(ipBxyz),lb+1,nZeta,HerW(iHerW(nHer)),nHer,Array(ipTemp3))

! Combine the cartesian components to the full one electron integral.

ip = ipAlph
do iBeta=1,nBeta
  call dcopy_(nAlpha,Alpha,1,Array(ip),1)
  ip = ip+nAlpha
end do
ip = ipBeta
do iAlpha=1,nAlpha
  call dcopy_(nBeta,Beta,1,Array(ip),nAlpha)
  ip = ip+1
end do
call CmbnRF1(Array(ipRnxyz),nZeta,la,lb,nOrdOp,Zeta,rKappa,rFinal,nComp,Array(ipTemp1),Array(ipTemp2),Array(ipAlph),Array(ipBeta), &
             Grad,nGrad,DAO,IfGrad,IndGrd,dc(mdc)%nStab,dc(ndc)%nStab,kOp,MM(1,2))

return

end subroutine RFGrd

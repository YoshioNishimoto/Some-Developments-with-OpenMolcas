#***********************************************************************
# This file is part of OpenMolcas.                                     *
#                                                                      *
# OpenMolcas is free software; you can redistribute it and/or modify   *
# it under the terms of the GNU Lesser General Public License, v. 2.1. *
# OpenMolcas is distributed in the hope that it will be useful, but it *
# is provided "as is" and without any express or implied warranties.   *
# For more details see the full text of the license in the file        *
# LICENSE or in <http://www.gnu.org/licenses/>.                        *
#                                                                      *
# Copyright (C) 2017,2018, Leon Freitag                                *
#***********************************************************************
#                                                                      *
# Based on HDF5_QCM Makefile by Stefan Knecht                          *
#                                                                      *
#***********************************************************************
# CMakeLists.txt for NEVPT2                                            *
#***********************************************************************

# load External Project macro
include(ExternalProject)
# Set up compilation of QCMaquis components
set(CUSTOM_NEVPT2_LOCATION ${PROJECT_BINARY_DIR}/External/nevpt2_ext)
set(CUSTOM_QCMaquis_LOCATION ${PROJECT_BINARY_DIR}/External/qcmaquis-full)

# QCMaquis does not know profile
if(CMAKE_BUILD_TYPE MATCHES "profile")
  set(NEVPT2_BUILD_TYPE "release")
else()
  set(NEVPT2_BUILD_TYPE ${CMAKE_BUILD_TYPE})
endif()

list(APPEND CMAKE_MODULE_PATH ${CMAKE_ROOT})
list(APPEND CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/cmake/custom)

set(CMAKE_Fortran_MODULE_DIRECTORY ${PROJECT_BINARY_DIR}/mod)

# enforce Intel MKL only if set for Molcas as well
if(LINALG STREQUAL "MKL")
        set(ENABLE_INTEL_MKL ON)
        list(APPEND NEVPT2CMakeArgs
            -DMKLROOT=${MKLROOT} )
else()
        set(ENABLE_INTEL_MKL OFF)
endif()

list(APPEND NEVPT2CMakeArgs
        -DCMAKE_BUILD_TYPE=${NEVPT2_BUILD_TYPE}
        -DCMAKE_INSTALL_PREFIX=${PROJECT_BINARY_DIR}/External
        -DCMAKE_Fortran_COMPILER=${CMAKE_Fortran_COMPILER}
        -DCMAKE_Fortran_FLAGS=${CMAKE_Fortran_FLAGS}
        -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
        -DCMAKE_C_FLAGS=${CMake_C_FLAGS}
        -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
        -DCMAKE_INSTALL_LIBDIR=lib
        -DCMAKE_Fortran_MODULE_DIRECTORY=${CMAKE_Fortran_MODULE_DIRECTORY}
        -DENABLE_DEBUG_DMRG=OFF
        -DENABLE_DMRG=ON
        -DENABLE_OPENMP=${OPENMP}
        -DENABLE_INTEL_MKL=${ENABLE_INTEL_MKL}
        -DENABLE_MOLCAS=ON
        -DMOLCAS_BUILD_DIR=${PROJECT_BINARY_DIR}
  )
# feel free to add more flags


######################################
# Download NEVPT2                    #
######################################
set(EP_PROJECT nevpt2_ext)

# Enabling source changes to keep ExternalProject happy
set (CMAKE_DISABLE_SOURCE_CHANGES OFF)

set (NEVPT2_dl_cmd "sh" "-c" "${QCMaquis_dl_SCRIPT} ${QCMaquis_URL} nevpt2 \'${QCMaquis_GIVEN_NAME}\' \'${QCMaquis_SURNAME}\' ${QCMaquis_EMAIL} && cd ${EP_PROJECT} && tar xvjf ../nevpt2.tar.bz2")

if (QCMaquis_UPDATE)
  message (STATUS "QCMaquis update:" ${QCMaquis_UPDATE} "; Triggering NEVPT2 update")
  set (NEVPT2_up_cmd "sh" "-c" "cd ../ && rm nevpt2.tar.bz2 || true && ${QCMaquis_dl_SCRIPT} ${QCMaquis_URL} nevpt2 \'${QCMaquis_GIVEN_NAME}\' \'${QCMaquis_SURNAME}\' ${QCMaquis_EMAIL} && cd ${EP_PROJECT} && tar xvjf ../nevpt2.tar.bz2")
endif()

ExternalProject_Add(${EP_PROJECT}
                    PREFIX ${CUSTOM_NEVPT2_LOCATION}
                    DOWNLOAD_COMMAND ${NEVPT2_dl_cmd}
                    UPDATE_COMMAND ${NEVPT2_up_cmd}
                    CMAKE_ARGS ${NEVPT2CMakeArgs}
                    INSTALL_DIR ${PROJECT_BINARY_DIR}
                    LOG_DOWNLOAD 1
                    LOG_UPDATE 1
                    LOG_CONFIGURE 1
                    LOG_INSTALL 1
                   )

set (CMAKE_DISABLE_SOURCE_CHANGES ON)

ExternalProject_Add_Step(${EP_PROJECT} install_tools DEPENDEES update
                   # Copy distributed 4-RDM tools
                   COMMAND ${CMAKE_COMMAND} -E copy_directory ${CUSTOM_QCMaquis_LOCATION}/src/qcmaquis-driver/tools/distributed-4rdm ${PROJECT_BINARY_DIR}/Tools/distributed-4rdm
                   # Copy the OpenMOLCAS tests
                   COMMAND ${CMAKE_COMMAND} -E copy_directory ${CUSTOM_NEVPT2_LOCATION}/src/${EP_PROJECT}/test ${PROJECT_BINARY_DIR}/qcmaquis/test/
                   COMMENT "Copying tools for distributed 4-RDM calculation and tests for NEVPT2"
                   )

# set variables for use in parent CMakeLists.txt
ExternalProject_Get_Property(${EP_PROJECT} BINARY_DIR)

set(NEVPT2_INCLUDE ${CMAKE_Fortran_MODULE_DIRECTORY} PARENT_SCOPE)
set(NEVPT2_LIBRARIES ${BINARY_DIR}/${CMAKE_FIND_LIBRARY_PREFIXES}qdnevpt2.a ${BINARY_DIR}/${CMAKE_FIND_LIBRARY_PREFIXES}AUXLIB_F.a PARENT_SCOPE)
